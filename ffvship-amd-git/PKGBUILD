_plug=ffvship-amd
pkgname=${_plug}-git
pkgrel=1
pkgver=3.0.0.13.g1ed2fb3
pkgdesc="Cli tool for computing the metric difference between two videos (GIT version)"
arch=('x86_64')
url='https://github.com/Line-fr/Vship'
license=('MIT')
depends=()
makedepends=('git' 'make' 'clang' 'hip-runtime-amd')
provides=("${_plug}")
conflicts=("${_plug}")
source=("${_plug}::git+https://github.com/Line-fr/Vship.git")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${_plug}"

  export PATH="/opt/rocm/bin:/opt/rocm/lib/llvm/bin:${PATH}"
  export LDFLAGS="-L/opt/rocm/lib ${LDFLAGS:-}"
  export LD_LIBRARY_PATH="/opt/rocm/lib:${LD_LIBRARY_PATH:-}"

  make buildFFVSHIPall \
      CXXFLAGS="-I/opt/rocm/include" \
      LDFLAGS="-L/opt/rocm/lib"
}

pkgver() {
  cd "${srcdir}/${_plug}"
  # Generate pkgver like: v3.0.0.0.g7cd1d4f
  git describe --tags --long | sed 's/^v//;s/-/./g'
}

package(){
  cd "${srcdir}/${_plug}"
  make PREFIX="/usr" DESTDIR="${pkgdir}" install
}
