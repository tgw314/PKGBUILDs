_plug=vship-amd
pkgname=vapoursynth-plugin-${_plug}-git
pkgrel=1
pkgver=3.0.0.13.g1ed2fb3
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('x86_64')
url='https://github.com/Line-fr/Vship'
license=('MIT')
depends=('vapoursynth' 'ffms2' 'hip-runtime-amd')
makedepends=('git' 'hip-runtime-amd')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/Line-fr/Vship.git")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${_plug}"

  export PATH="/opt/rocm/bin:/opt/rocm/lib/llvm/bin:${PATH}"
  export HIP_PATH=/opt/rocm

  make build
}

pkgver() {
  cd "${srcdir}/${_plug}"
  # Generate pkgver like: v3.0.0.0.g7cd1d4f
  git describe --tags --long | sed 's/^v//;s/-/./g'
}

package(){
  cd "${srcdir}/${_plug}"
  make PREFIX="/usr" DESTDIR="${pkgdir}" install
}
